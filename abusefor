#!/usr/bin/gawk -f
# "abusefor" by Peter Gamache, Feb 2012. (http://gplus.luno.org)
# Released to the public domain.  Feel free to reuse the "Abusix(ip)" function!
#
# Purpose: Obtain abuse contact(s) for IP as either argument or on STDIN/file
# If called with an IP as an argument, exit immediately (with 0 on success, 1 on failure)

function Abusix(ip) {
  split(ip,I,"[.]");
  q=sprintf("host -t txt %s.%s.%s.%s.abuse-contacts.abusix.org 2>/dev/null", I[4],I[3],I[2],I[1]);
  i=0;
  delete EMAIL;

  # Actual query happens here
  while ((q | getline)>0) {
    if (index($0, " descriptive text ")) {
      gsub("\042","",$NF);
      EMAIL[i++]=$NF;
    }
  }
  close(q);

  # process the results
  if (i > 0) { # success!
    i=0;
    rcpt="";
    while (i in EMAIL) {
      if (i>0) rcpt=rcpt ",";
      rcpt=rcpt EMAIL[i++];
    }
    printf("%s\n", rcpt);
    return(0);
  } else {
    return(1);
  }
}

# Handle command line argument...
BEGIN { if (length(ARGV[1])>0) { exit(Abusix(ARGV[1])); } }

# Or if we're still running, handle file/STDIN.
/^[0-9]+[.][0-9]+[.][0-9]+[.][0-9]+/ { Abusix($1); }

#EOF
  
